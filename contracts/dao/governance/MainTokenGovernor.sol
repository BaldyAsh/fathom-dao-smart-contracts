// SPDX-License-Identifier: MIT
// Copyright Fathom 2022

pragma solidity 0.8.16;

import "./Governor.sol";
import "./extensions/GovernorSettings.sol";
import "./extensions/GovernorCountingSimple.sol";
import "./extensions/GovernorVotes.sol";
import "./extensions/GovernorVotesQuorumFraction.sol";
import "./extensions/GovernorTimelockControl.sol";
import "../tokens/ERC20/IERC20.sol";
import "../../common/SafeERC20.sol";

contract MainTokenGovernor is
    Governor,
    GovernorSettings,
    GovernorCountingSimple,
    GovernorVotes,
    GovernorVotesQuorumFraction,
    GovernorTimelockControl
{
    using SafeERC20 for IERC20;
    mapping(address => bool) public isSupportedToken;

    constructor(
        IVotes _token,
        TimelockController _timelock,
        address _multiSig,
        uint256 _initialVotingDelay,
        uint256 _votingPeriod,
        uint256 _initialProposalThreshold,
        uint256 _proposalTimeDelay,
        uint256 _proposalLifetime
    )
        Governor("MainTokenGovernor", _multiSig, 20, _proposalTimeDelay,_proposalLifetime)
        GovernorSettings(_initialVotingDelay, _votingPeriod, _initialProposalThreshold)
        GovernorVotes(_token)
        GovernorVotesQuorumFraction(4)
        GovernorTimelockControl(_timelock)
    {}

    function propose(
        address[] memory targets,
        uint256[] memory values,
        bytes[] memory calldatas,
        string memory description
    ) public override(Governor, IGovernor) returns (uint256) {
        return super.propose(targets, values, calldatas, description);
    }

    function cancelProposal(
        address[] memory targets,
        uint256[] memory values,
        bytes[] memory calldatas,
        bytes32 descriptionHash
    ) public override onlyMultiSig returns (uint256) {
        return _cancel(targets, values, calldatas, descriptionHash);
    }

    function proposalThreshold() public view override(Governor, GovernorSettings) returns (uint256) {
        return super.proposalThreshold();
    }

    function supportsInterface(bytes4 interfaceId) public view override(Governor, GovernorTimelockControl) returns (bool) {
        return super.supportsInterface(interfaceId);
    }

    function votingDelay() public view override(IGovernor, GovernorSettings) returns (uint256) {
        return super.votingDelay();
    }

    function votingPeriod() public view override(IGovernor, GovernorSettings) returns (uint256) {
        return super.votingPeriod();
    }

    function quorum(uint256 blockNumber) public view override(IGovernor, GovernorVotesQuorumFraction) returns (uint256) {
        return super.quorum(blockNumber);
    }

    function state(uint256 proposalId) public view override(Governor, GovernorTimelockControl) returns (ProposalState) {
        return super.state(proposalId);
    }

    function addSupportingToken(address _token) public onlyGovernance {
        require(!isSupportedToken[_token], "Token already supported");
        isSupportedToken[_token] = true;
    }

    function removeSupportingToken(address _token) public onlyGovernance {
        require(isSupportedToken[_token], "Token is not supported");
        isSupportedToken[_token] = false;
    }

    /**
     * @dev Relays a transaction or function call to an arbitrary target. In cases where the governance executor
     * is some contract other than the governor itself, like when using a timelock, this function can be invoked
     * in a governance proposal to recover tokens or Ether that was sent to the governor contract by mistake.
     * Note that if the executor is simply the governor itself, use of `relay` is redundant.
     */
    function relayERC20(
        address target,
        bytes calldata data
    ) external payable virtual onlyGovernance {
        require(isSupportedToken[target], "relayERC20: token not supported");
        (bool success, bytes memory returndata) = target.call(data);
        Address.verifyCallResult(success, returndata, "Governor: relayERC20 reverted without message");
    }

        /**
     * @dev Relays a transaction or function call to an arbitrary target. In cases where the governance executor
     * is some contract other than the governor itself, like when using a timelock, this function can be invoked
     * in a governance proposal to recover tokens or Ether that was sent to the governor contract by mistake.
     * Note that if the executor is simply the governor itself, use of `relay` is redundant.
     */
    function relayETH(
        address target,
        uint256 value,
        bytes calldata data
    ) external payable virtual onlyGovernance {
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        Address.verifyCallResult(success, returndata, "Governor: relayETH reverted without message");
    }

    function _execute(
        uint256 proposalId,
        address[] memory targets,
        uint256[] memory values,
        bytes[] memory calldatas,
        bytes32 descriptionHash
    ) internal override(Governor, GovernorTimelockControl) {
        require(isConfirmed[proposalId], "MainTokenGovernor: Proposal not confirmed by council");
        super._execute(proposalId, targets, values, calldatas, descriptionHash);
    }

    function _cancel(
        address[] memory targets,
        uint256[] memory values,
        bytes[] memory calldatas,
        bytes32 descriptionHash
    ) internal override(Governor, GovernorTimelockControl) returns (uint256) {
        return super._cancel(targets, values, calldatas, descriptionHash);
    }

    function _executor() internal view override(Governor, GovernorTimelockControl) returns (address) {
        return super._executor();
    }
}
